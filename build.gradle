plugins {
    id 'java'
    id 'maven'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
}

group 'io.feaggle'
version '2.0.0'

sourceCompatibility = 8

repositories {
    mavenCentral()
    jcenter()
}

ext {
    feaggle = '1.0.1'
    junit = '5.4.0'
    h2 = '1.4.199'
    flyway = '5.2.4'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

dependencies {
    compile "io.feaggle:feaggle:$feaggle"

    testCompile "org.flywaydb:flyway-core:$flyway"
    testCompile "com.h2database:h2:$h2"
    testCompile "org.junit.jupiter:junit-jupiter:$junit"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacoco {
    toolVersion = "0.8.3"
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}